#! perl6

use TAP;

my $path-sep = $*DISTRO.path-sep;

multi listall(IO::Path $path where .d) {
	return $path.dir(:test(rx/\.t$/)).flatmap(&listall);
}
multi listall(IO::Path $path) {
	return ~$path;
}

sub MAIN(Bool :l($lib), Bool :b($blib), Bool :$timer, Int :j($jobs) = 1, *@files) {
	die "No files given to run" if not @files;

	my @extra = (%*ENV<PERL6LIB> // '').split($path-sep);
	@extra.unshift($*CWD ~ '/lib') if $lib;
	@extra.unshift($*CWD ~ '/blib/lib') if $blib;
	%*ENV<PERL6LIB> = @extra.join($path-sep) if @extra;

	my @sources = @files.map(*.IO).flatmap(&listall);
	my $harness = TAP::Harness.new(:@sources).run(:$jobs, :$timer);
	my $int = signal(SIGINT).tap(-> { $harness.kill("Interrupted"); $int.close(); });
	return min($harness.result.failed, 254);
}
